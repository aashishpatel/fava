apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

group = 'com.flightstats'
version = '0.1.14-SNAPSHOT'

repositories {
    mavenCentral()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'fava'
                packaging 'jar'
                artifactId 'fava'
                description 'Fava is Flightstats\' contribution back to the open source java community.'
                url 'https://github.com/flightstats/fava'

                scm {
                    url 'scm:git@github.com:flightstats/fava.git'
                    connection 'scm:git@github.com:flightstats/fava.git'
                    developerConnection 'scm:git@github.com:flightstats/fava.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jkwatson'
                        name 'John Watson'
                    }
                    developer {
                        id 'breedx2'
                        name 'Jason Plumb'
                    }
                }
            }
        }
    }
}


dependencies {
    compile 'javax.inject:javax.inject:1'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.projectlombok:lombok:1.14.4'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.guava:guava:17.0'
    compile 'org.apache.httpcomponents:httpclient:4.5'
    compile 'org.apache.httpcomponents:httpmime:4.5'
    compile 'com.github.rholder:guava-retrying:1.0.6'
    compile 'com.amazonaws:aws-java-sdk:1.8.7'

    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
}
